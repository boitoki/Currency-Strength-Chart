// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© boitoki

//@version=5
//@see https://currency-strength.com/
//@see https://currency-strength.com/about.html
indicator('Currency Strength Chart', overlay=false, precision=2, max_labels_count=100, max_lines_count=100)

// import
import boitoki/AwesomeColor/3 as ac
import boitoki/ma_function/3 as ma

////////////////////
// Defines
////////////////////
noneColor = color.new(color.black, 100)
GP1 = 'General'
GP2 = 'Display'
GP3 = 'Colors'
GP4 = 'Panel'
GP5 = 'Source'
GP6 = 'Options'
GP7 = 'Line style'
GP8 = 'Label style'
GP9 = 'Currency pair strength'
EUR_FLAG = 'ðŸ‡ªðŸ‡º'
USD_FLAG = 'ðŸ‡ºðŸ‡¸'
JPY_FLAG = 'ðŸ‡¯ðŸ‡µ'
CHF_FLAG = 'ðŸ‡¨ðŸ‡­'
GBP_FLAG = 'ðŸ‡¬ðŸ‡§'
AUD_FLAG = 'ðŸ‡¦ðŸ‡º'
CAD_FLAG = 'ðŸ‡¨ðŸ‡¦'
NZD_FLAG = 'ðŸ‡³ðŸ‡¿'
t_reso_customize    = 'Resolution'
t_reso_lookback     = 'Lookback periods'
t_reso_auto         = 'Auto'
t_calc_every        = 'every ticks'
t_calc_after        = 'after the bar closed'
t_histogram_option1 = 'Lines only'
t_histogram_option2 = 'Histogram'
t_histogram_option3 = 'Without lines'

////////////////////
// Functions
////////////////////
get_value_by_name (v, _EUR, _USD, _JPY, _CHF, _GBP, _AUD, _CAD, _NZD) =>
	v == 'EUR' ? _EUR : v == 'USD' ? _USD : v == 'JPY' ? _JPY : v == 'CHF' ? _CHF : v == 'GBP' ? _GBP : v == 'AUD' ? _AUD : v == 'CAD' ? _CAD : v == 'NZD' ? _NZD : na

get_name_by_value(v, _EUR, _USD, _JPY, _CHF, _GBP, _AUD, _CAD, _NZD) =>
    v == _EUR ? 'EUR' : v == _USD ? 'USD' : v == _JPY ? 'JPY' : v == _CHF ? 'CHF' : v == _GBP ? 'GBP' : v == _AUD ? 'AUD' : v == _CAD ? 'CAD' : v == _NZD ? 'NZD' : na

get_flag_by_value(v, _EUR, _USD, _JPY, _CHF, _GBP, _AUD, _CAD, _NZD) =>
    v == _EUR ? EUR_FLAG : v == _USD ? USD_FLAG : v == _JPY ? JPY_FLAG : v == _CHF ? CHF_FLAG : v == _GBP ? GBP_FLAG : v == _AUD ? AUD_FLAG : v == _CAD ? CAD_FLAG : v == _NZD ? NZD_FLAG : na

get_pair_by_value (_v, _EURUSD, _USDJPY, _USDCHF, _GBPUSD, _AUDUSD , _USDCAD, _NZDUSD) =>
    _v == _EURUSD ? 'EUR\nUSD' : _v == _USDJPY ? 'USD\nJPY' : _v == _USDCHF ? 'USD\nCHF' : _v == _GBPUSD ? 'GBP\nUSD' : _v == _AUDUSD ? 'AUD\nUSD' : _v == _USDCAD ? 'USD\nCAD' : _v == _NZDUSD ? 'NZD\nUSD' : na

f_reso (_mode, _auto, _input) => 
    switch _mode
        t_reso_auto => _auto
        t_reso_lookback => timeframe.period
        t_reso_customize => _input
        => _input

f_calculated_by (_type, _source, _length, _smooth) =>
    switch _type
        'RSI' => ta.rsi(_source, _length)
        'CCI' => ta.cci(_source, _length) + 1000
        'Stochastics' => ta.sma(ta.stoch(_source, high, low, _length), _smooth)
        => _source

f_color (_name, _a, _b, _c, _d, _e) =>
    switch _name
        'monokai' => _a
        'monokaipro' => _b
        'panda' => _c
        'trading view' => _d
        => _e

f_matched_currency_a_by_ticker (_name) => str.startswith(syminfo.ticker, _name)

f_matched_name_by_ticker (_name) => str.contains(syminfo.ticker, _name)

////////////////////
// Inputs
////////////////////
reso_autovalue = not timeframe.isintraday ? '12M' : timeframe.multiplier >= 60 ? 'W' : timeframe.multiplier >= 240 ? 'M' : 'D'
i_reso_mode = input.string(defval=t_reso_auto, title='Mode', options=[t_reso_auto, t_reso_customize, t_reso_lookback], group=GP1)
i_reso_value = input.timeframe(defval='D', title='Resolution', group=GP1, tooltip='When you choose the "Resolution" on the mode.')
i_reso_lookback = i_reso_mode == t_reso_lookback ? input.int(defval=100, title='Lookback periods', minval=1, group=GP1, tooltip='When you choose the "Lookback periods" on the mode.') : 1
i_broker = input.string(title='Brokers', options=['OANDA', 'FOREXCOM', 'SAXO', 'FX_IDC', 'FX'], defval='OANDA', group=GP1)
i_calclate = input.string(defval=t_calc_every, title='Calculated on', options=[t_calc_after, t_calc_every], group=GP1)

reso = f_reso(i_reso_mode, reso_autovalue, i_reso_value)
should_be_calclated = i_calclate == t_calc_after ? barstate.isconfirmed : true

////////////////////
// Source
////////////////////
i_source = input.source(defval=close, title='Source', group=GP5)
i_calc = input.string(defval='Price', title='Calculated by', options=['Price', 'RSI', 'CCI', 'Stochastics'], inline='calc', group=GP5)
i_length = input.int(defval=7, title='', inline='calc', group=GP5, tooltip='A value that is valid for non `Price`.')
i_smoothtype = input.string(defval='SMA', title='Smoothing by', options=['SMA', 'EMA', 'RMA', 'WMA', 'VWMA', 'HMA', 'LSMA', 'ALMA', 'SWMA', 'TMA', 'DEMA', 'TEMA', 'MAV', 'KAMA', 'ZLEMA', 'AMA'], inline='smooth', group=GP5)
i_smooth = input.int(defval=1, title='', minval=1, inline='smooth', group=GP5)
source = f_calculated_by(i_calc, i_source, i_length, 3)

////////////////////
// Display
////////////////////
show_chart   = input.bool(true, 'Show charts', group=GP2)
is_autoSelect = input.string(defval='No', title='Auto selected', options=['Yes', 'No'], inline='10', group=GP2) == 'Yes'
is_histogram = input.string(t_histogram_option1, title='', options=[t_histogram_option1, t_histogram_option2, t_histogram_option3], inline='10', group=GP2)
is_histogram_without_lines = is_histogram == t_histogram_option3

show_lines = show_chart ? (not is_histogram_without_lines) : false
show_fill = show_chart ? (show_lines and is_autoSelect) : false
show_histogram = show_chart ? (is_histogram != t_histogram_option1) : false

show_EUR    = input.bool(title='EUR', defval=true, group=GP2, inline='eur')
c_eur       = input.color(title='', defval=color.red, group=GP2, inline='eur')
i_eur_width = input.int(title='', defval=2, options=[1,2,3,4,5,6,7,8,9,10], group=GP2, inline='eur')
show_USD    = input.bool(title='USD', defval=true, group=GP2, inline='usd')
c_usd       = input.color(title='', defval=color.orange, group=GP2, inline='usd')
i_usd_width = input.int(title='', defval=2, options=[1,2,3,4,5,6,7,8,9,10], group=GP2, inline='usd')
show_JPY    = input.bool(title='JPY', defval=true, group=GP2, inline='jpy')
c_jpy       = input.color(title='', defval=color.aqua, group=GP2, inline='jpy')
i_jpy_width = input.int(title='', defval=2, options=[1,2,3,4,5,6,7,8,9,10], group=GP2, inline='jpy')
show_GBP    = input.bool(title='GBP', defval=true, group=GP2, inline='gbp')
c_gbp       = input.color(title='', defval=color.green, group=GP2, inline='gbp')
i_gbp_width = input.int(title='', defval=2, options=[1,2,3,4,5,6,7,8,9,10], group=GP2, inline='gbp')
show_CHF    = input.bool(title='CHF', defval=true, group=GP2, inline='chf')
c_chf       = input.color(title='', defval=color.maroon, group=GP2, inline='chf')
i_chf_width = input.int(title='', defval=2, options=[1,2,3,4,5,6,7,8,9,10], group=GP2, inline='chf')
show_AUD    = input.bool(title='AUD', defval=true, group=GP2, inline='aud')
c_aud       = input.color(title='', defval=color.blue, group=GP2, inline='aud')
i_aud_width = input.int(title='', defval=2, options=[1,2,3,4,5,6,7,8,9,10], group=GP2, inline='aud')
show_CAD    = input.bool(title='CAD', defval=true, group=GP2, inline='cad')
c_cad       = input.color(title='', defval=color.purple, group=GP2, inline='cad')
i_cad_width = input.int(title='', defval=2, options=[1,2,3,4,5,6,7,8,9,10], group=GP2, inline='cad')
show_NZD    = input.bool(title='NZD', defval=true, group=GP2, inline='nzd')
c_nzd       = input.color(title='', defval=color.fuchsia, group=GP2, inline='nzd')
i_nzd_width = input.int(title='', defval=2, options=[1,2,3,4,5,6,7,8,9,10], group=GP2, inline='nzd')
show_XAU    = input.bool(title='XAU', defval=false,group=GP2, inline='xau')
c_xau       = input.color(title='', defval=#D7BE69, group=GP2, inline='xau')
i_xau_width = input.int(title='', defval=2, options=[1,2,3,4,5,6,7,8,9,10], group=GP2, inline='xau')
show_XAG    = input.bool(title='XAG', defval=false, group=GP2, inline='xag')
c_xag       = input.color(title='', defval=color.silver, group=GP2, inline='xag')
i_xag_width = input.int(title='', defval=2, options=[1,2,3,4,5,6,7,8,9,10], group=GP2, inline='xag')

i_theme = input.string(title="Color theme", defval="monokai", options=["monokai", "monokai pro", "panda", "trading view", "customize"], group=GP2)
i_histogram_color = input.string(defval="Flat", title="Histogram color is", options=['Flat', 'Depth'], group=GP2)
should_be_text_black = (i_theme == 'monokai' or i_theme == 'monokai pro' or i_theme == 'panda')

if barstate.isfirst and is_autoSelect
    show_EUR := f_matched_name_by_ticker('EUR'), show_USD := f_matched_name_by_ticker('USD'), show_JPY := f_matched_name_by_ticker('JPY')
    show_GBP := f_matched_name_by_ticker('GBP'), show_CHF := f_matched_name_by_ticker('CHF'), show_AUD := f_matched_name_by_ticker('AUD')
    show_CAD := f_matched_name_by_ticker('CAD'), show_NZD := f_matched_name_by_ticker('NZD')
    show_XAU := false, show_XAG := false


var float v1_EURUSD = na, var float v2_EURUSD = na
var float v1_USDJPY = na, var float v2_USDJPY = na
var float v1_USDCHF = na, var float v2_USDCHF = na
var float v1_GBPUSD = na, var float v2_GBPUSD = na
var float v1_AUDUSD = na, var float v2_AUDUSD = na
var float v1_USDCAD = na, var float v2_USDCAD = na
var float v1_NZDUSD = na, var float v2_NZDUSD = na
var float v1_XAUUSD = na, var float v2_XAUUSD = na
var float v1_XAGUSD = na, var float v2_XAGUSD = na

v1_EURUSD := should_be_calclated ? request.security(i_broker + ':EURUSD', timeframe.period, source) : v1_EURUSD[1]
v1_USDJPY := should_be_calclated ? request.security(i_broker + ':USDJPY', timeframe.period, source) : v1_USDJPY[1]
v1_USDCHF := should_be_calclated ? request.security(i_broker + ':USDCHF', timeframe.period, source) : v1_USDCHF[1]
v1_GBPUSD := should_be_calclated ? request.security(i_broker + ':GBPUSD', timeframe.period, source) : v1_GBPUSD[1]
v1_AUDUSD := should_be_calclated ? request.security(i_broker + ':AUDUSD', timeframe.period, source) : v1_AUDUSD[1]
v1_USDCAD := should_be_calclated ? request.security(i_broker + ':USDCAD', timeframe.period, source) : v1_USDCAD[1]
v1_NZDUSD := should_be_calclated ? request.security(i_broker + ':NZDUSD', timeframe.period, source) : v1_NZDUSD[1]
v1_XAUUSD := should_be_calclated and show_XAU ? request.security(i_broker + ':XAUUSD', timeframe.period, source) : v1_XAUUSD[1]
v1_XAGUSD := should_be_calclated and show_XAG ? request.security(i_broker + ':XAGUSD', timeframe.period, source) : v1_XAGUSD[1]

v2_EURUSD := should_be_calclated ? request.security(i_broker + ':EURUSD', reso, source[i_reso_lookback], barmerge.gaps_off, barmerge.lookahead_on) : v2_EURUSD[1]
v2_USDJPY := should_be_calclated ? request.security(i_broker + ':USDJPY', reso, source[i_reso_lookback], barmerge.gaps_off, barmerge.lookahead_on) : v2_USDJPY[1]
v2_USDCHF := should_be_calclated ? request.security(i_broker + ':USDCHF', reso, source[i_reso_lookback], barmerge.gaps_off, barmerge.lookahead_on) : v2_USDCHF[1]
v2_GBPUSD := should_be_calclated ? request.security(i_broker + ':GBPUSD', reso, source[i_reso_lookback], barmerge.gaps_off, barmerge.lookahead_on) : v2_GBPUSD[1]
v2_AUDUSD := should_be_calclated ? request.security(i_broker + ':AUDUSD', reso, source[i_reso_lookback], barmerge.gaps_off, barmerge.lookahead_on) : v2_AUDUSD[1]
v2_USDCAD := should_be_calclated ? request.security(i_broker + ':USDCAD', reso, source[i_reso_lookback], barmerge.gaps_off, barmerge.lookahead_on) : v2_USDCAD[1]
v2_NZDUSD := should_be_calclated ? request.security(i_broker + ':NZDUSD', reso, source[i_reso_lookback], barmerge.gaps_off, barmerge.lookahead_on) : v2_NZDUSD[1]
v2_XAUUSD := should_be_calclated and show_XAU ? request.security(i_broker + ':XAUUSD', reso, source[i_reso_lookback], barmerge.gaps_off, barmerge.lookahead_on) : v2_XAUUSD[1]
v2_XAGUSD := should_be_calclated and show_XAU ? request.security(i_broker + ':XAGUSD', reso, source[i_reso_lookback], barmerge.gaps_off, barmerge.lookahead_on) : v2_XAGUSD[1]

////////////////////
// Other settings
////////////////////

// Labels
i_label_shows = input.bool(title='Name label', defval=true, inline='name_label', group=GP8)
i_label_size = input.string(size.normal, '', options=[size.auto, size.tiny, size.small, size.normal, size.large, size.huge], inline='name_label', group=GP8)
i_line_style = input.string(line.style_dotted, '', options=[line.style_solid, line.style_dotted, line.style_dashed], inline="name_label", group=GP8)
i_label_offset = input.int(5, '', minval=0, inline='name_label', group=GP8)
i_label_style = label.style_label_left

x1_label = bar_index + i_label_offset
x1_line  = bar_index
x2_line  = x1_label

// Panel
i_panel_show = input.bool(title='Show Panel', defval=true, group=GP4)
i_panel_flagsize = input.string(title='Flag', defval=size.large, options=[size.auto, size.tiny, size.small, size.normal, size.large, size.huge], inline='panel_display', group=GP4)
i_panel_textsize = input.string(title='Text', defval=size.normal, options=[size.auto, size.tiny, size.small, size.normal, size.large, size.huge], inline='panel_display', group=GP4)
i_tableYpos = input.string('top', title='Position', options = ['top', 'middle', 'bottom'], inline='11', group=GP4)
i_tableXpos = input.string('right', title='', options=['left', 'center', 'right'], inline='11', group=GP4)
i_panel_position = i_tableYpos + '_' + i_tableXpos
i_panel_theme = 'multi'

// Currency pair strength
i_pair_show = input.bool(title="Currency pair strength", defval=false, inline='pair_display', group=GP9)
i_pair_color = str.lower(input.string(title='Color', defval='Dark', options=['Dark', 'light'], inline='pair_display', group=GP9))
i_pair_ypos = input.string('bottom', title='Position', options = ['top', 'middle', 'bottom'], inline='pair_position', group=GP9)
i_pair_xpos = input.string('right', title='', options=['left', 'center', 'right'], inline='pair_position', group=GP9)
i_pair_position = i_pair_ypos + '_' + i_pair_xpos


// Options
i_levels_show = input.bool(title='Level lines', defval=false, group=GP6)
i_levels = input.int(title='Level (Â±)', minval=1, defval=10, group=GP6)
c_levels = color.gray


// Color themes
c_green = ac.tradingview('green')
c_red   = ac.tradingview('red')
c_black = ac.tradingview('black')

if barstate.isfirst
    c_eur   := f_color(i_theme, ac.monokai('pink'), ac.monokaipro('pink'), ac.panda('red'), ac.tradingview_flag('red'), c_eur)
    c_usd   := f_color(i_theme, ac.monokai('orange'), ac.monokaipro('orange'), ac.panda('orange'), ac.tradingview_flag('orange'), c_usd)
    c_jpy   := f_color(i_theme, c_jpy, c_jpy, c_jpy, ac.tradingview_flag('cyan'), c_jpy)
    c_gbp   := f_color(i_theme, ac.monokai('green'), ac.monokaipro('green'), ac.panda('green'), ac.tradingview_flag('green'), c_gbp)
    c_cad   := f_color(i_theme, ac.monokai('purple'), ac.monokaipro('purple'), ac.panda('purple'), ac.tradingview_flag('purple'), c_cad)
    c_aud   := f_color(i_theme, ac.monokai('blue'), ac.monokaipro('blue'), ac.panda('blue'), ac.tradingview_flag('blue'), c_aud)
    c_green := f_color(i_theme, ac.monokai('green'), ac.monokaipro('green'), ac.panda('green'), ac.tradingview_flag('green'), c_green)
    c_red   := f_color(i_theme, ac.monokai('pink'), ac.monokaipro('pink'), ac.panda('red'), ac.tradingview_flag('red'), c_red)
    c_black := f_color(i_theme, ac.monokai('black'), ac.monokaipro('black'), ac.panda('very-dark-gray'), c_black, c_black)

////////////////////
// Functions
////////////////////
get_color_by_value(v, _EUR, _USD, _JPY, _CHF, _GBP, _AUD, _CAD, _NZD) =>
    v == _EUR ? c_eur : v == _USD ? c_usd : v == _JPY ? c_jpy : v == _CHF ? c_chf : v == _GBP ? c_gbp : v == _AUD ? c_aud : v == _CAD ? c_cad : v == _NZD ? c_nzd : na

get_val(_v1, _v2) =>
    float res = 0

    if _v2 == 0
        res := 0
        res
    else
        res := math.log(_v1 / _v2) * 10000
        res

    res

get_val_m(_v1, _v2, _v3, _v4) =>
    float res = 0
    float v1 = _v1 * _v3
    float v2 = _v2 * _v4

    if _v2 == 0
        res := 0
        res
    else
        res := math.log(v1 / v2) * 10000
        res

    res

get_val_d(_v1, _v2, _v3, _v4) =>
    float res = 0

    if _v3 == 0 or _v4 == 0
        res := 0
        res
    else
        float v1 = _v1 / _v3
        float v2 = _v2 / _v4

        if v2 == 0
            res := 0
            res
        else
            res := math.log(v1 / v2) * 10000
            res

    res

////////////////////
// Render functions
////////////////////
f_render_label (_show, _x1, _x2, _v, _n, _color, _label_color, _line_style, _label_style, _thin) =>
    var line myline = na, var label mylabel = na
    
    if _show
        x2 = _x1 + _x2
        myline  := line.new(_x1, _v, x2, _v, xloc.bar_index, extend.none, _color, _line_style, _thin), line.delete(myline[1])
        mylabel := label.new(x2, _v, _n, xloc.bar_index, yloc.price, _color, _label_style, _label_color, i_label_size, text.align_center, na), label.delete(mylabel[1])

f_render_levels (_show_charts, _show_lavels, _y, _color) =>
    var line line_base = na
    var line line_upper = na
    var line line_lower = na

    if barstate.isfirst
        x1 = bar_index
        x2 = bar_index + 10
        line_base  := _show_charts ? line.new(x1,  0 , x2,   0, color=_color, style=line.style_solid , extend=extend.both) : na, line.delete(line_base[1])
        line_upper := _show_lavels ? line.new(x1,  _y, x2,  _y, color=_color, style=line.style_dotted, extend=extend.both) : na, line.delete(line_upper[1])
        line_lower := _show_lavels ? line.new(x1, -_y, x2, -_y, color=_color, style=line.style_dotted, extend=extend.both) : na, line.delete(line_lower[1])

f_render_pair_panel (_show, _position, _colorscheme, _EURUSD, _USDJPY, _USDCHF, _GBPUSD, _AUDUSD, _USDCAD, _NZDUSD) =>
    v_values = array.from(_EURUSD, _USDJPY, _USDCHF, _GBPUSD, _AUDUSD, _USDCAD, _NZDUSD), array.sort(v_values, order.descending)

    if barstate.islast and _show
        var table mypanel = table.new(_position, 2, 8), table.clear(mypanel, 0, 0, 1,  7)
        c_ticker = _colorscheme == 'dark' ? ac.tradingview('lightgray') : ac.tradingview('black')
        c_pair_bull = ac.tradingview('green')
        c_pair_bear = ac.tradingview('red')
        c_pair_newt = ac.tradingview('gray')
        i_pair_strength = 30
        
        for i = 0 to array.size(v_values)-1
            v_value = array.get(v_values, i)
            v_label = get_pair_by_value(array.get(v_values, i), _EURUSD, _USDJPY, _USDCHF, _GBPUSD, _AUDUSD, _USDCAD, _NZDUSD)
            v_color = v_value >= 0 ? c_pair_bull : c_pair_bear
            v_color := math.abs(v_value) < i_pair_strength ? c_pair_newt : v_color
    
            table.cell(mypanel, 0, i, str.replace(v_label, '\n', ''), text_halign=text.align_left, text_color=c_ticker)
            table.cell(mypanel, 1, i, str.format('{0,number,#.##}',v_value), text_halign=text.align_right, text_color=v_color)

f_render_panel (_show, _position, _color, _EUR, _USD, _JPY, _CHF, _GBP, _AUD, _CAD, _NZD) =>
    v_values = array.new_float(8)
    v_sorted = array.new_string(8)

    if barstate.islast 
        v_values := array.from(_EUR, _USD, _JPY, _CHF, _GBP, _AUD, _CAD, _NZD)
        array.sort(v_values, order.descending)
    
        var table mypanel = table.new(_position, 4, 8, noneColor, noneColor, 0, noneColor, 0)
        table.clear(mypanel, 1, 0, 3, 7)
    
        for i = 0 to array.size(v_values) - 1 by 1
            v_value = array.get(v_values, i)
            n = get_name_by_value(v_value , _EUR, _USD, _JPY, _CHF, _GBP, _AUD, _CAD, _NZD)
            f = get_flag_by_value(v_value , _EUR, _USD, _JPY, _CHF, _GBP, _AUD, _CAD, _NZD)
            c = get_color_by_value(v_value, _EUR, _USD, _JPY, _CHF, _GBP, _AUD, _CAD, _NZD)
            v = str.format('{0, number, #.#}', v_value)
            r = str.tostring(i + 1)
        
            if not na(n)
                array.set(v_sorted, i, n)
        
            if _show
                head_bgcolor = c
                bgcolor      = c
                table.cell(mypanel, 0, i, r, bgcolor=head_bgcolor, text_color=_color, text_size=i_panel_textsize)
                table.cell(mypanel, 1, i, f, bgcolor=bgcolor, text_color=_color, text_size=i_panel_flagsize)
                table.cell(mypanel, 2, i, n, bgcolor=bgcolor, text_color=_color, text_size=i_panel_textsize, text_halign=text.align_left)
                table.cell(mypanel, 3, i, v, bgcolor=bgcolor, text_color=_color, text_size=i_panel_textsize, text_halign=text.align_right)

    [v_values, v_sorted]


////////////////////
// Calculating
////////////////////
var float EURUSD = na
var float USDJPY = na
var float USDCHF = na
var float GBPUSD = na
var float AUDUSD = na
var float USDCAD = na
var float NZDUSD = na
var float EURJPY = na
var float EURCHF = na
var float EURGBP = na
var float EURCAD = na
var float EURAUD = na
var float EURNZD = na
var float EURXAU = na
var float EURXAG = na
var float GBPCHF = na
var float GBPJPY = na
var float GBPCAD = na
var float GBPAUD = na
var float GBPNZD = na
var float AUDCHF = na
var float AUDJPY = na
var float AUDCAD = na
var float AUDNZD = na
var float NZDCAD = na
var float NZDCHF = na
var float NZDJPY = na
var float CADJPY = na
var float CADCHF = na
var float CHFJPY = na

var float XAUUSD = na
var float XAGUSD = na
var float XAUJPY = na
var float XAUCHF = na
var float XAUGBP = na
var float XAUCAD = na
var float XAUAUD = na
var float XAUNZD = na
var float XAGJPY = na
var float XAGCHF = na
var float XAGGBP = na
var float XAGCAD = na
var float XAGAUD = na
var float XAGNZD = na

EURUSD := should_be_calclated ? get_val(v1_EURUSD, v2_EURUSD) : EURUSD[1]
USDJPY := should_be_calclated ? get_val(v1_USDJPY, v2_USDJPY) : USDJPY[1]
USDCHF := should_be_calclated ? get_val(v1_USDCHF, v2_USDCHF) : USDCHF[1]
GBPUSD := should_be_calclated ? get_val(v1_GBPUSD, v2_GBPUSD) : GBPUSD[1]
AUDUSD := should_be_calclated ? get_val(v1_AUDUSD, v2_AUDUSD) : AUDUSD[1]
USDCAD := should_be_calclated ? get_val(v1_USDCAD, v2_USDCAD) : USDCAD[1]
NZDUSD := should_be_calclated ? get_val(v1_NZDUSD, v2_NZDUSD) : NZDUSD[1]
XAUUSD := should_be_calclated ? get_val(v1_XAUUSD, v2_XAUUSD) : XAUUSD[1]
XAGUSD := should_be_calclated ? get_val(v1_XAGUSD, v2_XAGUSD) : XAGUSD[1]
EURJPY := should_be_calclated ? get_val_m(v1_EURUSD, v2_EURUSD, v1_USDJPY, v2_USDJPY) : EURJPY[1]
EURCHF := should_be_calclated ? get_val_m(v1_EURUSD, v2_EURUSD, v1_USDCHF, v2_USDCHF) : EURCHF[1]
EURGBP := should_be_calclated ? get_val_d(v1_EURUSD, v2_EURUSD, v1_GBPUSD, v2_GBPUSD) : EURGBP[1]
EURCAD := should_be_calclated ? get_val_m(v1_EURUSD, v2_EURUSD, v1_USDCAD, v2_USDCAD) : EURCAD[1]
EURAUD := should_be_calclated ? get_val_d(v1_EURUSD, v2_EURUSD, v1_AUDUSD, v2_AUDUSD) : EURAUD[1]
EURNZD := should_be_calclated ? get_val_d(v1_EURUSD, v2_EURUSD, v1_NZDUSD, v2_NZDUSD) : EURNZD[1]
EURXAU := should_be_calclated ? get_val_d(v1_EURUSD, v2_EURUSD, v1_XAUUSD, v2_XAUUSD) : EURXAU[1]
EURXAG := should_be_calclated ? get_val_d(v1_EURUSD, v2_EURUSD, v1_XAGUSD, v2_XAGUSD) : EURXAG[1]
GBPCHF := should_be_calclated ? get_val_m(v1_GBPUSD, v2_GBPUSD, v1_USDCHF, v2_USDCHF) : GBPCHF[1]
GBPJPY := should_be_calclated ? get_val_m(v1_GBPUSD, v2_GBPUSD, v1_USDJPY, v2_USDJPY) : GBPJPY[1]
GBPCAD := should_be_calclated ? get_val_m(v1_GBPUSD, v2_GBPUSD, v1_USDCAD, v2_USDCAD) : GBPCAD[1]
GBPAUD := should_be_calclated ? get_val_d(v1_GBPUSD, v2_GBPUSD, v1_AUDUSD, v2_AUDUSD) : GBPAUD[1]
GBPNZD := should_be_calclated ? get_val_d(v1_GBPUSD, v2_GBPUSD, v1_NZDUSD, v2_NZDUSD) : GBPNZD[1]
AUDCHF := should_be_calclated ? get_val_m(v1_AUDUSD, v2_AUDUSD, v1_USDCHF, v2_USDCHF) : AUDCHF[1]
AUDJPY := should_be_calclated ? get_val_m(v1_AUDUSD, v2_AUDUSD, v1_USDJPY, v2_USDJPY) : AUDJPY[1]
AUDCAD := should_be_calclated ? get_val_m(v1_AUDUSD, v2_AUDUSD, v1_USDCAD, v2_USDCAD) : AUDCAD[1]
AUDNZD := should_be_calclated ? get_val_d(v1_AUDUSD, v2_AUDUSD, v1_NZDUSD, v2_NZDUSD) : AUDNZD[1]
NZDCAD := should_be_calclated ? get_val_m(v1_NZDUSD, v2_NZDUSD, v1_USDCAD, v2_USDCAD) : NZDCAD[1]
NZDCHF := should_be_calclated ? get_val_m(v1_NZDUSD, v2_NZDUSD, v1_USDCHF, v2_USDCHF) : NZDCHF[1]
NZDJPY := should_be_calclated ? get_val_m(v1_NZDUSD, v2_NZDUSD, v1_USDJPY, v2_USDJPY) : NZDJPY[1]
CADJPY := should_be_calclated ? get_val_d(v1_USDJPY, v2_USDJPY, v1_USDCAD, v2_USDCAD) : CADJPY[1]
CADCHF := should_be_calclated ? get_val_d(v1_USDCHF, v2_USDCHF, v1_USDCAD, v2_USDCAD) : CADCHF[1]
CHFJPY := should_be_calclated ? get_val_d(v1_USDJPY, v2_USDJPY, v1_USDCHF, v2_USDCHF) : CHFJPY[1]
XAUJPY := should_be_calclated ? get_val_m(v1_XAUUSD, v2_XAUUSD, v1_USDJPY, v2_USDJPY) : XAUJPY[1]
XAUCHF := should_be_calclated ? get_val_m(v1_XAUUSD, v2_XAUUSD, v1_USDCHF, v2_USDCHF) : XAUCHF[1]
XAUGBP := should_be_calclated ? get_val_d(v1_XAUUSD, v2_XAUUSD, v1_GBPUSD, v2_GBPUSD) : XAUGBP[1]
XAUCAD := should_be_calclated ? get_val_m(v1_XAUUSD, v2_XAUUSD, v1_USDCAD, v2_USDCAD) : XAUCAD[1]
XAUAUD := should_be_calclated ? get_val_d(v1_XAUUSD, v2_XAUUSD, v1_AUDUSD, v2_AUDUSD) : XAUAUD[1]
XAUNZD := should_be_calclated ? get_val_d(v1_XAUUSD, v2_XAUUSD, v1_NZDUSD, v2_NZDUSD) : XAUNZD[1]
XAGJPY := should_be_calclated ? get_val_m(v1_XAGUSD, v2_XAGUSD, v1_USDJPY, v2_USDJPY) : XAGJPY[1]
XAGCHF := should_be_calclated ? get_val_m(v1_XAGUSD, v2_XAGUSD, v1_USDCHF, v2_USDCHF) : XAGCHF[1]
XAGGBP := should_be_calclated ? get_val_d(v1_XAGUSD, v2_XAGUSD, v1_GBPUSD, v2_GBPUSD) : XAGGBP[1]
XAGCAD := should_be_calclated ? get_val_m(v1_XAGUSD, v2_XAGUSD, v1_USDCAD, v2_USDCAD) : XAGCAD[1]
XAGAUD := should_be_calclated ? get_val_d(v1_XAGUSD, v2_XAGUSD, v1_AUDUSD, v2_AUDUSD) : XAGAUD[1]
XAGNZD := should_be_calclated ? get_val_d(v1_XAGUSD, v2_XAGUSD, v1_NZDUSD, v2_NZDUSD) : XAGNZD[1]

EUR = ma.calc(i_smoothtype, ( EURUSD + EURJPY + EURCHF + EURGBP + EURAUD + EURCAD + EURNZD) / 7, i_smooth)
USD = ma.calc(i_smoothtype, (-EURUSD + USDJPY + USDCHF - GBPUSD - AUDUSD + USDCAD - NZDUSD) / 7, i_smooth)
JPY = ma.calc(i_smoothtype, (-EURJPY - USDJPY - CHFJPY - GBPJPY - AUDJPY - CADJPY - NZDJPY) / 7, i_smooth)
CHF = ma.calc(i_smoothtype, (-EURCHF - USDCHF + CHFJPY - GBPCHF - AUDCHF - CADCHF - NZDCHF) / 7, i_smooth)
GBP = ma.calc(i_smoothtype, (-EURGBP + GBPUSD + GBPCHF + GBPJPY + GBPAUD + GBPCAD + GBPNZD) / 7, i_smooth)
AUD = ma.calc(i_smoothtype, (-EURAUD + AUDUSD + AUDJPY + AUDCHF - GBPAUD + AUDCAD + AUDNZD) / 7, i_smooth)
CAD = ma.calc(i_smoothtype, (-EURCAD - USDCAD + CADJPY + CADCHF - GBPCAD - AUDCAD - NZDCAD) / 7, i_smooth)
NZD = ma.calc(i_smoothtype, (-EURNZD + NZDUSD + NZDJPY + NZDCHF - GBPNZD + NZDCAD - AUDNZD) / 7, i_smooth)
XAU = ma.calc(i_smoothtype, (-EURXAU + XAUUSD + XAUJPY + XAUCHF + XAUGBP + XAUAUD + XAUCAD + XAUNZD) / 8, i_smooth)
XAG = ma.calc(i_smoothtype, (-EURXAG + XAGUSD + XAGJPY + XAGCHF + XAGGBP + XAGAUD + XAGCAD + XAGNZD) / 8, i_smooth)

// Delta
//========
currency_a = str.substring(syminfo.ticker, 0, 3)
currency_b = str.substring(syminfo.ticker, 3)
currency_a_value = get_value_by_name(currency_a, EUR, USD, JPY, CHF, GBP, AUD, CAD, NZD)
currency_b_value = get_value_by_name(currency_b, EUR, USD, JPY, CHF, GBP, AUD, CAD, NZD)
currency_delta = currency_a_value - currency_b_value
chg_delta = ta.change(currency_delta)

c_currency_a = get_value_by_name(currency_a, c_eur, c_usd, c_jpy, c_chf, c_gbp, c_aud, c_cad, c_nzd)
c_currency_b = get_value_by_name(currency_b, c_eur, c_usd, c_jpy, c_chf, c_gbp, c_aud, c_cad, c_nzd)
c_delta = currency_delta > 0 ? c_currency_a : c_currency_b
c_delta_transparency = i_histogram_color == 'Depth' ? ((currency_delta > 0 and chg_delta < 0) or (currency_delta < 0 and chg_delta > 0) ? 65 : 40) :  40


////////////////////
// Plotting
////////////////////
// Histogram
plot(show_histogram ? currency_delta : na, color=color.new(c_delta, c_delta_transparency), style=plot.style_columns, editable=false)

// Lines
plot(show_XAG and show_lines? XAG : na, title='XAG', color=c_xag, linewidth=i_xag_width, editable=false)
plot(show_XAU and show_lines? XAU : na, title='XAU', color=c_xau, linewidth=i_xau_width, editable=false)
plot(show_NZD and show_lines? NZD : na, title='NZD', color=c_nzd, linewidth=i_nzd_width, editable=false)
plot(show_CAD and show_lines? CAD : na, title='CAD', color=c_cad, linewidth=i_cad_width, editable=false)
plot(show_CHF and show_lines? CHF : na, title='CHF', color=c_chf, linewidth=i_chf_width, editable=false)
plot(show_AUD and show_lines? AUD : na, title='AUD', color=c_aud, linewidth=i_aud_width, editable=false)
plot(show_GBP and show_lines? GBP : na, title='GBP', color=c_gbp, linewidth=i_gbp_width, editable=false)
plot(show_JPY and show_lines? JPY : na, title='JPY', color=c_jpy, linewidth=i_jpy_width, editable=false)
plot(show_EUR and show_lines? EUR : na, title='EUR', color=c_eur, linewidth=i_eur_width, editable=false)
plot(show_USD and show_lines? USD : na, title='USD', color=c_usd, linewidth=i_usd_width, editable=false)
plot(show_fill ? currency_a_value : na, title='A fill', color=color.new(c_currency_a, 92), linewidth=0, style=plot.style_area, editable=false)
plot(show_fill ? currency_b_value : na, title='B fill', color=color.new(c_currency_b, 92), linewidth=0, style=plot.style_area, editable=false)

// Lines & Labels
var line XAG_LINE = na, var label USD_LABEL = na
var line XAU_LINE = na, var label XAG_LABEL = na
var line NZD_LINE = na, var label XAU_LABEL = na
var line CAD_LINE = na, var label NZD_LABEL = na
var line CHF_LINE = na, var label CAD_LABEL = na
var line AUD_LINE = na, var label CHF_LABEL = na
var line GBP_LINE = na, var label AUD_LABEL = na
var line JPY_LINE = na, var label GBP_LABEL = na
var line EUR_LINE = na, var label JPY_LABEL = na
var line USD_LINE = na, var label EUR_LABEL = na

if barstate.islast and i_label_shows and show_lines
    c_label = should_be_text_black ? c_black : color.white

    f_render_label(show_XAG, bar_index, i_label_offset, XAG, 'XAG', c_xag, c_label, i_line_style, i_label_style, i_xag_width)
    f_render_label(show_XAU, bar_index, i_label_offset, XAU, 'XAU', c_xau, c_label, i_line_style, i_label_style, i_xau_width)
    f_render_label(show_NZD, bar_index, i_label_offset, NZD, 'NZD', c_nzd, c_label, i_line_style, i_label_style, i_nzd_width)
    f_render_label(show_CAD, bar_index, i_label_offset, CAD, 'CAD', c_cad, c_label, i_line_style, i_label_style, i_cad_width)
    f_render_label(show_CHF, bar_index, i_label_offset, CHF, 'CHF', c_chf, c_label, i_line_style, i_label_style, i_chf_width)
    f_render_label(show_AUD, bar_index, i_label_offset, AUD, 'AUD', c_aud, c_label, i_line_style, i_label_style, i_aud_width)
    f_render_label(show_GBP, bar_index, i_label_offset, GBP, 'GBP', c_gbp, c_label, i_line_style, i_label_style, i_gbp_width)
    f_render_label(show_JPY, bar_index, i_label_offset, JPY, 'JPY', c_jpy, c_label, i_line_style, i_label_style, i_jpy_width)
    f_render_label(show_EUR, bar_index, i_label_offset, EUR, 'EUR', c_eur, c_label, i_line_style, i_label_style, i_eur_width)
    f_render_label(show_USD, bar_index, i_label_offset, USD, 'USD', c_usd, c_label, i_line_style, i_label_style, i_usd_width)

f_render_levels(show_lines, i_levels_show, i_levels, c_levels)

////////////////////
// Tables
////////////////////
// Currency pair strength
DELTA_EURUSD = (EUR-USD)
DELTA_USDJPY = (USD-JPY)
DELTA_USDCHF = (USD-CHF)
DELTA_GBPUSD = (GBP-USD)
DELTA_AUDUSD = (AUD-USD)
DELTA_USDCAD = (USD-CAD)
DELTA_NZDUSD = (NZD-USD)
//DELTA_EURJPY := (EUR-JPY)
//DELTA_EURCHF := (EUR-CHF)
//DELTA_EURGBP := (EUR-GBP)
//DELTA_EURCAD := (EUR-CAD)
//DELTA_EURAUD := (EUR-AUD)
//DELTA_EURNZD := (EUR-NZD)
//DELTA_GBPCHF := (GBP-CHF)
//DELTA_GBPJPY := (GBP-JPY)
//DELTA_GBPCAD := (GBP-CAD)
//DELTA_GBPAUD := (GBP-AUD)
//DELTA_GBPNZD := (GBP-NZD)
//DELTA_AUDCHF := (AUD-CHF)
//DELTA_AUDJPY := (AUD-JPY)
//DELTA_AUDCAD := (AUD-CAD)
//DELTA_AUDNZD := (AUD-NZD)
//DELTA_NZDCAD := (NZD-CAD)
//DELTA_NZDCHF := (NZD-CHF)
//DELTA_NZDJPY := (NZD-JPY)
//DELTA_CADJPY := (CAD-JPY)
//DELTA_CADCHF := (CAD-CHF)
//DELTA_CHFJPY := (CHF-JPY)

f_render_pair_panel(i_pair_show, i_pair_position, i_pair_color, DELTA_EURUSD, DELTA_USDJPY, DELTA_USDCHF, DELTA_GBPUSD, DELTA_AUDUSD, DELTA_USDCAD, DELTA_NZDUSD)

// Currency strength chart
[values, sorted_by_ranking_names] = f_render_panel(i_panel_show, i_panel_position, c_black, EUR, USD, JPY, CHF, GBP, AUD, CAD, NZD)


////////////////////
// Alerts
////////////////////
var string the_strongest_name = na
var string the_weakest_name = na

if barstate.islast and barstate.isrealtime
    float max_value = array.max(values)
    float min_value = array.min(values)
    the_strongest_name := get_name_by_value(max_value, EUR, USD, JPY, CHF, GBP, AUD, CAD, NZD)
    the_weakest_name := get_name_by_value(min_value, EUR, USD, JPY, CHF, GBP, AUD, CAD, NZD)
    bool strongest_value_changed = the_strongest_name != the_strongest_name[1]
    bool weakest_value_changed = the_weakest_name != the_weakest_name[1]
    string alert_message = array.join(sorted_by_ranking_names, ' > ')
    
    if strongest_value_changed or weakest_value_changed
        alert('NEW UPDATE: ' + alert_message, alert.freq_once_per_bar_close)
